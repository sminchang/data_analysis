@echo off
setlocal enabledelayedexpansion

rem 기본 변수 설정
set count=1

rem "processed" 폴더가 없으면 생성
if not exist "processed" mkdir "processed"

rem 파일 확장자 및 접두어 입력 받기
set /p "file_pattern=처리할 파일 확장자를 입력하세요 (예: pdf): "
set /p "prefix=새 파일명의 접두어를 입력하세요 (예: 2021_01): "

rem 새 파일명의 초기 순번 입력 받기
set /p "start_number=새 파일명의 접미어(초기 순번)을 입력하세요 (예: 1): "
set count=%start_number%

rem PowerShell을 사용하여 파일 목록 정렬 후 처리
for /f "delims=" %%f in ('powershell -command "Get-ChildItem -Path . -Filter *.%file_pattern% | Sort-Object {[regex]::Replace($_.Name, '\d+', {$args[0].Value.PadLeft(20)})} | ForEach-Object { $_.Name }"') do (
    rem 점미어 5자리 숫자로 포맷 설정
    set "formatted_count=00000!count!"
    set "formatted_count=!formatted_count:~-5!"

    rem 임시 파일명으로 이름 변경 후 processed 폴더로 이동
    ren "%%f" "temp_!formatted_count!.%file_pattern%"
    move "temp_!formatted_count!.%file_pattern%" "processed\"

    rem 새 파일명으로 변경
    ren "processed\temp_!formatted_count!.%file_pattern%" "%prefix%_!formatted_count!.%file_pattern%"

    rem 변경된 파일명 출력
    echo Renamed %%f to %prefix%_!formatted_count!.%file_pattern%

    rem 카운트 증가
    set /a count+=1
)

pause
